@EndUserText.label: 'Custom Entity for Upload Job Table Rows'
@ObjectModel.query.implementedBy: 'ABAP:ZCL_CE_XA_UPLOAD_TABLE_ROW'
@Search.searchable: true

define custom entity Zce_xa_upload_table_rows

{
      @UI         : {
        selectionField: [{position: 10}],
        lineItem  : [{position: 10, importance: #HIGH }]
      }

      @Search.defaultSearchElement: true
  key JobId       : zxa_upload_jobid;

      @UI         : {
        selectionField: [{position: 20}],
        lineItem  : [{position: 20, importance: #HIGH }]
      }

      @Search.defaultSearchElement: true
  key Scenario    : zxa_upload_scenario;

      @UI         : {
        selectionField: [{position: 30}],
        lineItem  : [{position: 30, importance: #HIGH }]
      }

      @Search.defaultSearchElement: true
      @Semantics.text: true
  key Version     : zxa_upload_version;

      @UI         : {
        selectionField: [{position: 40}],
        lineItem  : [{position: 40, importance: #HIGH }]
      }

      @Search.defaultSearchElement: true
  key TableId     : zxa_upload_tableid;

      @UI         : {
        selectionField: [{position: 50}],
        lineItem  : [{position: 50, importance: #HIGH }]
      }

      @Search.defaultSearchElement: true
  key RecordId    : zxa_upload_recordid;

      @UI         : {
        selectionField: [{position: 60}],
        lineItem  : [{position: 60, importance: #HIGH }]
      }

      @Search.defaultSearchElement: true
      Status      : zxa_upload_status;


      @UI         : {
      selectionField: [{position: 70}],
      lineItem    : [{position: 70, importance: #HIGH }]
      }

      Description : zxa_upload_description;
      
      MessageTypeMax : abap.char(1); //E=error, W=Warning, 

      @ObjectModel.sort.enabled   : false
      @ObjectModel.filter.enabled : false
      _Job        : association to parent Zce_xa_upload_jobs on $projection.JobId = _Job.JobId;

      @ObjectModel.sort.enabled: false
      @ObjectModel.filter.enabled: false
      @Consumption.filter.hidden: false
      _Values     : composition [0..*] of Zce_xa_upload_table_values;

      @ObjectModel.sort.enabled: false
      @ObjectModel.filter.enabled: false
      @Consumption.filter.hidden: false
      _Messages   : composition [0..*] of Zce_xa_upload_table_msgs;
      
      @ObjectModel.sort.enabled: false
      @ObjectModel.filter.enabled: false
      @Consumption.filter.hidden: true
      _Status     : association [1] to ZCE_XA_UPLOAD_STATUS_VALUES on _Status.Status = $projection.Status;
      
}
