CLASS zcl_ce_ps_cats_absence DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_rap_query_provider .
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS ZCL_CE_PS_CATS_ABSENCE IMPLEMENTATION.


  METHOD if_rap_query_provider~select.

    DATA: lt_absences TYPE TABLE OF zce_ps_cats_absence,
          ls_absence  TYPE zce_ps_cats_absence,
          lt_return   TYPE TABLE OF bapiret2.

    DATA: lv_employeenumber TYPE zce_ps_cats_day-employeenumber,
          lv_WeekStartDate  TYPE zce_ps_cats_day-WeekStartDate.

    TRY.
        DATA(lo_instance) = zcl_xa_sys_helper=>get_instance(  ).

        " Get the ECC on-premise destination
        DATA(lv_destination_new)  = lo_instance->get_sap_onpremise_destination(  ).

        IF io_request IS BOUND.
          " Map the OData request parameters to the FM importing/table objects
          lo_instance->map_rap_io_request_params( EXPORTING io_request = io_request
                                                  IMPORTING es_pagination = DATA(ls_pagination)
                                                            et_filters = DATA(lt_filter)
                                                            et_parameters = DATA(lt_parameters)
                                                            et_requested_elements = DATA(lt_requested_elements)
                                                            et_sort = DATA(lt_sort)
                                                            ev_search_expression = DATA(lv_search_expression) ).
        ENDIF.

        "Read filters
        LOOP AT lt_filter ASSIGNING FIELD-SYMBOL(<fs_filter>).
          LOOP AT <fs_filter>-single_select ASSIGNING FIELD-SYMBOL(<fs_single_select>).
            IF <fs_single_select>-range IS NOT INITIAL.
              CASE <fs_single_select>-name.
                WHEN 'EMPLOYEENUMBER'.
                  lv_employeenumber = <fs_single_select>-range[ 1 ]-low.
                WHEN 'STARTDATE'.
                  lv_WeekStartDate = <fs_single_select>-range[ 1 ]-low.
              ENDCASE.
            ENDIF.
          ENDLOOP.
        ENDLOOP.

      CATCH cx_rfc_dest_provider_error INTO DATA(lx_dest).

        RAISE EXCEPTION TYPE zxa_sys_rap_provider_exception
          EXPORTING
            textid   = zxa_sys_rap_provider_exception=>destination_provider_error
            previous = lx_dest.

    ENDTRY.


    IF io_request->is_data_requested(  ).

      CALL FUNCTION 'Z_BE_PS_CATS_GET_ABSENCES' DESTINATION lv_destination_new
        EXPORTING
          iv_startdate          = lv_weekstartdate
          iv_employeenumber     = lv_employeenumber
        IMPORTING
          et_absences           = lt_absences
          et_return             = lt_return
        EXCEPTIONS
          system_failure        = 1
          communication_failure = 2
          OTHERS                = 99.

      CASE sy-subrc.
        WHEN 0.
*            Check for errors
            zcl_ce_ps_cats_util=>handle_bapiret2_return( CHANGING ct_return = lt_return ).

        WHEN 1.
          RAISE EXCEPTION TYPE zxa_sys_rap_provider_exception
            EXPORTING
              textid   = zxa_sys_rap_provider_exception=>rfc_sytem_failure
              rfc_name = 'Z_BE_PS_CATS_GET_ABSENCES'.
        WHEN 2.
          RAISE EXCEPTION TYPE zxa_sys_rap_provider_exception
            EXPORTING
              textid   = zxa_sys_rap_provider_exception=>rfc_communication_failure
              rfc_name = 'Z_BE_PS_CATS_GET_ABSENCES'.
        WHEN 99.
          RAISE EXCEPTION TYPE zxa_sys_rap_provider_exception
            EXPORTING
              textid   = zxa_sys_rap_provider_exception=>rfc_unknown_failure
              rfc_name = 'Z_BE_PS_CATS_GET_ABSENCES'.
      ENDCASE.


      io_response->set_data( lt_absences ).
    ENDIF.

    IF ( io_request->is_total_numb_of_rec_requested(  ) ).
      io_response->set_total_number_of_records( lines( lt_absences ) ).
    ENDIF.
  ENDMETHOD.
ENDCLASS.