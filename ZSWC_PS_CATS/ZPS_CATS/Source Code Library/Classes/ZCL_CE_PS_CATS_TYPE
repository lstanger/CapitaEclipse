CLASS zcl_ce_ps_cats_type DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES if_rap_query_provider .
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS ZCL_CE_PS_CATS_TYPE IMPLEMENTATION.


  METHOD if_rap_query_provider~select.

    DATA: lt_types          TYPE TABLE OF zce_ps_cats_type,
          lt_all_types      TYPE TABLE OF zce_ps_cats_type,
          ls_type           TYPE zce_ps_cats_type,
          lv_type           LIKE ls_type-typekey,
          lv_employeenumber LIKE ls_type-employeenumber.

    TRY.
        DATA(lo_instance) = zcl_xa_sys_helper=>get_instance(  ).

        " Get the ECC on-premise destination
        DATA(lv_destination_new)  = lo_instance->get_sap_onpremise_destination(  ).

        IF io_request IS BOUND.
          " Map the OData request parameters to the FM importing/table objects
          lo_instance->map_rap_io_request_params( EXPORTING io_request = io_request
                                                  IMPORTING es_pagination = DATA(ls_pagination)
                                                            et_filters = DATA(lt_filter)
                                                            et_parameters = DATA(lt_parameters)
                                                            et_requested_elements = DATA(lt_requested_elements)
                                                            et_sort = DATA(lt_sort)
                                                            ev_search_expression = DATA(lv_search_expression) ).
        ENDIF.

        LOOP AT lt_filter ASSIGNING FIELD-SYMBOL(<fs_filter>).
          LOOP AT <fs_filter>-single_select ASSIGNING FIELD-SYMBOL(<fs_single_select>).
            IF <fs_single_select>-range IS NOT INITIAL.
              CASE <fs_single_select>-name.
                WHEN 'TYPEKEY'.
                  lv_type = <fs_single_select>-range[ 1 ]-low.
                WHEN 'EMPLOYEENUMBER'.
                  lv_employeenumber = <fs_single_select>-range[ 1 ]-low.
              ENDCASE.
            ENDIF.
          ENDLOOP.

          IF io_request->is_data_requested( ).
            CLEAR: lt_types.

            CALL FUNCTION 'Z_BE_PS_CATS_GET_TYPE' DESTINATION lv_destination_new
              EXPORTING
                iv_employeenumber     = lv_employeenumber
                iv_typekey            = lv_type
              IMPORTING
                et_types              = lt_types
              EXCEPTIONS
                system_failure        = 1
                communication_failure = 2
                OTHERS                = 99.

            CASE sy-subrc.
              WHEN 0.
                append lines of lt_types to lt_all_types.

              WHEN 1.
                RAISE EXCEPTION TYPE zxa_sys_rap_provider_exception
                  EXPORTING
                    textid   = zxa_sys_rap_provider_exception=>rfc_sytem_failure
                    rfc_name = 'Z_BE_PS_CATS_GET_TYPE'.
              WHEN 2.
                RAISE EXCEPTION TYPE zxa_sys_rap_provider_exception
                  EXPORTING
                    textid   = zxa_sys_rap_provider_exception=>rfc_communication_failure
                    rfc_name = 'Z_BE_PS_CATS_GET_TYPE'.
              WHEN 99.
                RAISE EXCEPTION TYPE zxa_sys_rap_provider_exception
                  EXPORTING
                    textid   = zxa_sys_rap_provider_exception=>rfc_unknown_failure
                    rfc_name = 'Z_BE_PS_CATS_GET_TYPE'.
            ENDCASE.


          ENDIF.
        ENDLOOP.

      CATCH cx_rfc_dest_provider_error INTO DATA(lx_dest).

        RAISE EXCEPTION TYPE zxa_sys_rap_provider_exception
          EXPORTING
            textid   = zxa_sys_rap_provider_exception=>destination_provider_error
            previous = lx_dest.

    ENDTRY.


    IF io_request->is_data_requested(  ).
      io_response->set_data( lt_all_types ).
    ENDIF.

    IF ( io_request->is_total_numb_of_rec_requested(  ) ).
      io_response->set_total_number_of_records( lines( lt_all_types ) ).
    ENDIF.
  ENDMETHOD.
ENDCLASS.